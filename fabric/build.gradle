plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
/*
architectury {
    platformSetupLoomIde()
    fabric()
}*/

loom {
    accessWidenerPath = project(":").loom.accessWidenerPath
}
base {
    archivesName = archivesName.get() + '-fabric'
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    common(project(path: ":", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":", configuration: "namedElements")) { transitive false }
    // fixme 此 shadowCommon 在 Architectury Plugin 中的 configuration 为 transformProductionFabric，这在 Fabric Loom 中不存在，这会导致 shadow 中的 mixin 无法自动添加 refmap，需要手动添加。

    modImplementation "com.terraformersmc:modmenu:${rootProject.mod_menu_version}"
}

processResources {
    // 确保这里的是模组版本（不含游戏版本）而不是项目版本
    inputs.property "version", rootProject.mod_version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.mod_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = rootProject.sourcesJar
    dependsOn commonSources
    duplicatesStrategy DuplicatesStrategy.INCLUDE

    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
    }
}