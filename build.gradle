plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+" apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
}

architectury {
    minecraft = rootProject.minecraft_version
    common((rootProject.enabled_platforms as String).split(","))
}

allprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version + "-" + project.minecraft_version
    group = rootProject.maven_group

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven {
            url 'https://maven.aliyun.com/nexus/content/groups/public'
        }
    }
    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        // 由于新版的 Loom 没有 @CanIgnoreReturnValue 等注解，故在这里手动添加。
        modCompileOnly "com.google.errorprone:error_prone_annotations:+"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 8
    }

    java {
        withSourcesJar()
    }
}

archivesBaseName += '-common'

subprojects {
    apply plugin: "com.modrinth.minotaur"

    loom {
        runs {
            client {
                programArg "-username=Solid"
                runDir = '../run'
            }
            server {
                runDir = '../run'
            }
        }
    }

    publishing {
        repositories {
            maven {
                url = rootProject.file('../mvn-repo').path
            }
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/fabric-data-generation-api.accesswidener")
}
dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archives_base_name
            from components.java
        }
    }
    repositories {
    }
}